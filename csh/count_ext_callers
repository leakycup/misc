#!/bin/csh -f

if ($#argv < 4) then
    goto USAGE
endif

set caller_file = ""
set symbol = ""
set detail = ""
set help = ""

set last_keyword = ""
foreach arg ($*)
    if ("$last_keyword" == "-symbol") then
	set symbol = "$arg"
    endif
    if ("$last_keyword" == "-callers") then
	set caller_file = "$arg"
    endif
    if ("$arg" == "-detail") then
	set detail = "detail"
    endif
    if ("$arg" == "-help") then
	set help = "help"
    endif
    set last_keyword = "$arg"
end

if ($help != "") then
    goto USAGE
endif

set GREP = "/router/bin/grep"
set VIEWS = "sobhatta-redoak_infra sobhatta-hoodoo_infra sobhatta-eveready_infra sobhatta-flo_dsgs7_infra"

if (!(-e "$caller_file" && -r "$caller_file")) then
    echo "Could not locate callers file $caller_file"
    echo "Exiting..."
    exit 1
endif

$GREP -e "^WAYTOOMANYCALLERS: $symbol" "$caller_file" > /dev/null
if ($status == 0) then
    echo "$symbol : WAYTOOMANYCALLERS"
    exit 1
endif

set uniq_callers = `cat "$caller_file" | $GREP -e "^$symbol" | cut -f '3,4' -d ' ' | sort | uniq | wc -l`
if ($uniq_callers > 1500) then
    echo "$symbol : WAYTOOMANYCALLERS"
    exit 1
endif

set num_callers = 0
foreach caller ("`cat "$caller_file" | $GREP -e "^$symbol" | cut -f '3,4' -d ' ' | sort | uniq`")
    set caller_count = 0
    foreach view ($VIEWS)
	set callers_view = `cat "$caller_file" | $GREP -e "^$symbol $view $caller" | wc -l`
	if ($callers_view > $caller_count) then
	    set caller_count = $callers_view
	endif
	if ($detail != "") then
	    echo "$symbol $view $caller $callers_view"
	endif
    end
    @ num_callers += $caller_count
    if ($detail != "") then
	echo "$symbol $caller $caller_count"
	echo ""
    endif
end

echo "$symbol : $num_callers"

exit 0
USAGE:
echo "Usage:"
echo ""
echo "$0 : Get number of unique callers of a given symbol across 4 branches (redoak_infra, eveready_infra, hoodoo_infra, flo_dsgs7_infra)"
echo ""
echo "    $0 -callers <callers-file> -symbol <symbol-name> [-detail]"
echo ""
echo "            -callers <callers-file>  : caller file generated by /users/sobhatta/bin/external_callers. This file has callers' info for a set of symbols belonging to a vertical (os-core, os-infra, network-infra and comm-infra). For every caller of a symbol, the callers file has a line of the following form : <Symbol><space><View><space><CallerFile><space><Caller>"
echo ""
echo "            -symbol <symbol-name>    : Name of the symbol whose caller information is needed."
echo ""
echo "            -detail                  : [Optional] In addition to the caller count, also print information about every caller in a view in the following form : <Symbol><space><view><space><CallerFile><space><Caller><space><count>"
echo ""

exit 0
