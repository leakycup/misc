#!/bin/csh -f

if ($#argv < 2) then
    echo "Usage:"
    echo "$0 <disparity-file-name> <caller-impact-file-name>"
    echo "<disparity-file-name> : output of find_disparity_new tool"
    echo "<caller-impact-file-name>: output of caller_impact tool"
    exit 1
endif

set disparity_file = $1
set callerimp_file = $2

set VIEWS = "sobhatta-redoak_infra sobhatta-hoodoo_infra sobhatta-eveready_infra sobhatta-flo_dsgs7_infra"
set TMP = tmp

foreach line ("`cat $disparity_file | sort | uniq`")
    echo "$line" | cut -f 2 -d ' ' | grep 'y' > /dev/null
    if ($status != 0) continue
    set symbol = `echo "$line" | cut -f 1 -d ' '`
    grep "^$symbol : WAYTOOMANYCALLERS" $callerimp_file > /dev/null
    if ($status == 0) then
	echo "$symbol : WAYTOOMANYCALLERS"
	continue
    endif
    foreach view ($VIEWS)
	grep "^$symbol $view" "$callerimp_file" >! $TMP
	foreach comp ("`cat $TMP | cut -f 3 -d ' ' | sort | uniq`")
	    set comp_cnt = `grep $comp $TMP | wc -l`
	    set file_cnt = `grep $comp $TMP | sort | uniq | wc -l`
	    echo "$symbol $view $comp $comp_cnt $file_cnt"
	end
    end
end # foreach line of disparity file
